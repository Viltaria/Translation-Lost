define @master as Master

launch
  say "Welcome to Lost in Translation, a game where you try and understand me through my different accents."
  if @tutorialHeard == true
    say "Do you want to hear the tutorial again?"
    -> askForTutorial
  else
    -> playTutorial
  
playTutorial 
  say "I'm going to say a phrase, and you have to guess what I say."
  say "For example, if I say"
  soundEffect "bag_of_potatoes.mp3"
  say "You would translate to,"
  soundEffect "bag_of_potatoes2.mp3"

  say "You can ask me for help by saying, Alexa, help. Or ask me to repeat the phrase by saying, repeat."
  say "You can give up on a phrase by saying, Alexa, skip."
  say "You can relist all of these options during the game by saying, Alexa, options."
  @tutorialHeard = true
  -> getTopic

askForTutorial
  when AMAZON.YesIntent
    -> playTutorial
  when AMAZON.NoIntent
    say "Alright, let's get into it."
    -> getTopic
  otherwise
    say "Do you want to hear the tutorial again?"
    -> waitForAnswer

getTopic
  say "The current topic right now is, Movie Quotes."
  say "You'll have five minutes to score as many points as you can, with each translation correct being a point."
  say "Ready?"
  say "<sfx starto.mp3>"
    or "<!batter up>"
  @gameStartedTime = context.now
  @score = 0
  @gameInProgress = true
  -> generateRandomSpeech
 
generateRandomSpeech
  if @master.speechAvailable() == false
    say "No more translations available,"
    say "Goodbye."
    -> goodbye  
  else
    @speechKey = @master.getRandomSpeech()

    if @master.seenBefore(@speechKey)
      -> generateRandomSpeech
    else
      @master.seen(@speechKey)
      @temp = "https://lost-in-translation.s3.amazonaws.com/lost-in-translation/development/default/" + @speechKey +  '.mp3'
      -> askForAnswer

askForAnswer
  if minutesBetween(context.now, @gameStartedTime) >= 5
    say "Times up!"
    say "You scored @score points."
    -> askForPlayAgain
  else 
    say "\<audio src='@temp' />"

    say "What do you think I said?"
      or "What did I say?"

    # add an automatic re-prompt, in case the user says nothing
    reprompt "\<audio src='@temp' />"
    -> waitForAnswer

askForRepeat
  say "\<audio src='@temp' />"
  reprompt "\<audio src='@temp' />"
  -> waitForAnswer

askForPlayAgain
  say "Do you want to play again?"
  -> waitForPlayAgainAnswer

waitForPlayAgainAnswer
  when AMAZON.YesIntent
    say "Alright. Let's go again."
    say "Your last score was @score points."
    -> getTopic
  when AMAZON.NoIntent
    say "Goodbye."
    -> goodbye
  otherwise
    say "Do you want to play again?"
    -> waitForPlayAgainAnswer

giveHint
  log 'giveHint'
  if @master.hintAvailable(@speechKey) == false
    say "There are no more hints."
    -> waitForAnswer
  else
    @currentHint = @master.getHint(@speechKey)

    if @master.seenHintBefore(@speechKey, @currentHint)
      -> giveHint
    else 
      @master.seenHint(@speechKey, @currentHint)
      say "@currentHint."
      -> waitForAnswer


waitForAnswer
  when "it's $answer"
    or "$answer"
    with $answer = Master.js:getPotentialAnswers
    if $answer and $answer == @master.getAnswer(@speechKey)
      card "@master.getAnswer(@speechKey)"
        content: "@master.getAnnotation(@speechKey)"
      say "Correcto!"
        or "Nice job."
        or "<!awesome>."
        or "<!aww yeah>."
        or "<!bada bing bada boom>."
        or "<!bingo>."
        or "<!bazinga>."
        or "<!well done>."
        or "<!wowza>."
        or "<!wowzer>."
        or "About time!"
      say "@master.getAnswer(@speechKey)."
      say "@master.getAnnotation(@speechKey)."
      say "Let's go again."
        or "One more time!"
        or "Keep going!"
      @score = @score + 1
      -> generateRandomSpeech
    else
      say "<!aw man>."
        or "Come on, this one's easy."
        or "Wrong!"
        or "<!aww applesauce>. That isn't it."
        or "Correct! ... <!just kidding>."
        or  "<!nuh uh>."
        or "Nice Try!"
        or "You really don't know this?"
        or "Uncultured swine."
        or "Yikes."
        or "L. O. L." 
        or "Are you even trying?"
        or "Are you serious?"
      say "Here it is again."
        or "Try again!"
      -> askForRepeat

  when "what $blank is this"
    or  "$blank"
    with $blank = "accent"
    if $blank
      say "@master.getAccent(@speechKey)."
      -> waitForAnswer
    else
      say "Wrong!"
      say "Here it is again."
      -> askForRepeat

  when "i give up"
    or "i have no idea"
    or "skip this one"
    card "@master.getAnswer(@speechKey)"
      content: "@master.getAnnotation(@speechKey)"
    say "the answer was, @master.getAnswer(@speechKey)."
    say "@master.getAnnotation(@speechKey)."
    say "Let's go again."
      or "One more time."
    -> generateRandomSpeech

  when "repeat the $blankthree"
    with $blankthree = "hints"
    if $blankthree
      say "@currentHint."
      -> waitForAnswer
    else
      say "Wrong! Here it is again."
      -> askForRepeat
  
  when "list the $blankfour"
    or "$blankfour"
    with $blankfour = "options"
    if $blankfour
      say "You can ask me for help by saying, Alexa, help. Or ask me to repeat the phrase by saying, repeat."
      say "If you want to know the accent, ask me, Alexa, what accent is this."
      say "You can give up on a phrase by saying, Alexa, I give up."
    -> askForAnswer

  when AMAZON.RepeatIntent
    or "one more time"
    or "say again"
    -> askForRepeat

  when AMAZON.HelpIntent
    or "can i get a hint"
    or "hint please"
    or "hint"
    -> giveHint

  when AMAZON.StopIntent
    if @gameInProgress == true
      say "Are you sure you want to quit?"
      -> confirmGoodbye
    else
      -> goodbye

  otherwise
    say "Sorry, I didn't understand. If you want a hint, say, Alexa, help, otherwise say, Alexa, then the answer."
    say "If you want to hear all the options, say, Alexa, options."
    -> waitForAnswer

confirmGoodbye
  when AMAZON.YesIntent
    -> goodbye
  when AMAZON.NoIntent
    -> waitForAnswer
  otherwise
    say "Sorry, I didn't understand. Are you sure you want to quit?"
    -> confirmGoodbye

goodbye
  @gameInProgress = false
  END

